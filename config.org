#+title: Doom emacs configuration

This is a literate doom emacs config file.

* Core Setup
** Tangle Headers
#+begin_src elisp :tangle config.el
;; -*- lexical-binding: t; -*-
#+end_src

#+begin_src elisp :tangle packages.el
;; -*- no-byte-compile: t; -*-
;;; $DOOMDIR/packages.el
#+end_src

Don't do init.el
# #+begin_src elisp :tangle "init.el" :comments no
# ;; -*- no-byte-compile: t; -*-
# #+end_src

** Helper Functions
*** Helper for authinfo
#+begin_src elisp :tangle config.el
(require 'auth-source)

(defun get-auth-info (&rest params)
  (let ((match (car (apply 'auth-source-search params))))
    (if match
        (let ((secret (plist-get match :secret)))
          (if (functionp secret)
              (funcall secret)
            secret))
      (error "Password not found for %S" params))))
#+end_src

** Basic Settings
*** Grep no ask for save
#+begin_src elisp :tangle config.el
(setq grep-save-buffers nil)
#+end_src
*** Don't ask to close buffers
#+begin_src elisp tangle config.el
(setq confirm-kill-processes nil)
#+end_src
*** Don't ignore .bin files (I'm a firmware dev)
#+begin_src elisp :tangle config.el
(setq completion-ignored-extensions
      (delete ".bin" completion-ignored-extensions))
(after! dired-omit-mode
  (setq dired-omit-extensions
        (delete ".bin" dired-omit-extensions)))
#+end_src

* Appearance & UI
** Theme and Fonts
I like this theme as it has a lot of not washed out colors.
Week starts at monday here, despite what the Bible says.

#+begin_src elisp :tangle config.el
(setq doom-theme 'tsdh-dark
      display-line-numbers-type t
      calendar-week-start-day 1
      doom-font (font-spec :family "FiraCode Nerd Font" :size 15)
      )
(doom/set-frame-opacity 85 't)
#+end_src

** Visual Enhancements
*** Indent bars
#+begin_src elisp :tangle config.el
(after! indent-bars
  (setq
   indent-bars-color '(highlight :face-bg t :blend 0.15)
   indent-bars-pattern "."
   indent-bars-width-frac 0.1
   indent-bars-pad-frac 0.1
   indent-bars-zigzag nil
   indent-bars-color-by-depth '(:regexp "outline-\\([0-9]+\\)" :blend 1) ; blend=1: blend with BG only
   indent-bars-highlight-current-depth '(:blend 0.5) ; pump up the BG blend on current
   indent-bars-display-on-blank-lines t)
  )
#+end_src

* Org Mode
** Basic Configuration
*** org-mode dirs
#+begin_src elisp :tangle config.el
(after! org
  (setq org-directory "~/org/")
  )
#+end_src

*** Look and feel
#+begin_src elisp :tangle config.el
(after! org
  (setq org-indent-indentation-per-level 4)
  (defun append-org-todo-keyword (new-keyword)
    "Append a NEW-KEYWORD to the first sequence in `org-todo-keywords`."
    (let ((current-keywords (car org-todo-keywords)))
      (when (eq (car current-keywords) 'sequence)
        (setq org-todo-keywords
              (cons (append current-keywords (list new-keyword))
                    (cdr org-todo-keywords))))))
  
  (append-org-todo-keyword "REVIEW")
  
  (setq org-todo-keyword-faces
        (append org-todo-keyword-faces
                '(("REVIEW" . (:foreground "light blue" :weight bold))
                  ("DONE" . (:foreground "green" :weight bold :strike-through t)))))
  (custom-set-faces '(org-drawer ((t (:foreground "grey" :weight bold :height: 1.3)))))
  
  (setq org-global-properties
        '(("Effort_all" . "0:05 0:10 0:15 0:20 0:30 0:45 1:00 1:30 2:00 3:00 4:00 5:00 6:00 7:00")))
  
  (setq org-log-done 'time
        org-log-into-drawer "LOGBOOK")
  )
#+end_src
**** modern-indent
make "src" blocks look really good.
#+begin_src emacs-lisp :tangle packages.el
(package! org-modern-indent :recipe (:host github :repo "jdtsmith/org-modern-indent") :pin "9973bd3b91e4733a3edd1fca232208c837c05473")
#+end_src
#+begin_src emacs-lisp :tangle config.el
(add-hook 'org-mode-hook #'org-modern-indent-mode 90)
#+end_src

** Export & Integration
*** Org ol-gnus
Add gnus support
#+begin_src elisp :tangle config.el
(after! org
  (add-to-list 'org-modules 'ol-gnus)
  )
#+end_src

*** Org-ql
This allows to index org-mode quickly. Might be a good alternative to org-agenda
#+begin_src elisp :tangle packages.el
(package! org-ql)
#+end_src

** Agenda & Scheduling
*** habits
#+begin_src elisp :tangle config.el
(after! org
  (add-to-list 'org-modules 'org-habit)
  (setq org-habit-show-all-today t)
  )
#+end_src
*** org-window-habit (disabled)
Show habit consistency in a dedicated window while navigating agenda items.
#+begin_src elisp :tangle no
(package! org-window-habit :recipe (:host github :repo "colonelpanic8/org-window-habit") :pin "f326697899f3c67257137dc482a52f5d1b4acc10")
#+end_src
#+begin_src elisp :tangle no
(require 'org-window-habit)
(progn (org-window-habit-mode +1))
(setq org-window-habit-repeat-to-deadline nil)
#+end_src
*** timer config
Somehow emacs sound does not work. Use aplay instead.
#+begin_src elisp :tangle config.el
(after! org
  (defun my/org-play-sound (file)
    "Play a sound FILE using aplay."
    (start-process "aplay-sound" nil "aplay" file))
  
  (defun my/org-clock-out-sound ()
    (my/org-play-sound "/home/arthur/Music/ding0.wav"))
  
  (add-hook 'org-timer-done-hook #'my/org-clock-out-sound :append)
  (setq org-clock-sound nil)
  )
#+end_src
*** Agenda format
#+begin_src elisp :tangle config.el
(after! org
  (defun get-agenda-category (&optional len)
    "Get category of item at point for agenda.

Category is defined by one of the following items:
- CATEGORY property
- TITLE keyword
- filename without directory and extension

When LEN is a number, resulting string is padded right with
spaces and then truncated with ... on the right if result is
longer than LEN."
    (let* ((file-name (when buffer-file-name
                        (file-name-sans-extension
                         (file-name-nondirectory buffer-file-name))))
           (title (org-with-point-at 1
                    (when (re-search-forward (concat "^#\\+title: \\(.*\\)")
                                             (point-max) t)
                      (buffer-substring-no-properties
                       (match-beginning 1)
                       (match-end 1)))))
           (category (org-get-category))
           (result
            (or (if (and title
                         (string-equal category file-name))
                    title
                  category)
                "")))
      (if (numberp len)
          (s-truncate len (s-pad-right len " " result))
        result)))
  
  (setq org-agenda-prefix-format
        '((agenda . "   %i %(get-agenda-category 16)%?-2t %-12s %-6e")
          (todo .   "   %i %(get-agenda-category 16) %-12:t %s")
          (tags .   "   %i %(get-agenda-category 16) %-12:t %s")
          (search . "   %i %(get-agenda-category 16) %s")))
  )
#+end_src

*** Super agenda setup
#+begin_src elisp :tangle packages.el
(package! org-super-agenda)
#+end_src

#+begin_src elisp :tangle config.el
(after! org
  (add-hook 'org-agenda-mode-hook 'org-super-agenda-mode)
  (setq org-super-agenda-groups
        '((:log t)
          (:name "Schedule"
           :time-grid t)
          (:name "Habit: ora"
           :and (:habit t
                 :tag "PRAYER")
           )
          (:name "Habit: corpus"
           :and (:habit t
                 :tag "LICHAAM")
           )
          (:habit t)
          (:name "Today: labora"
           :and (:scheduled today
                 :todo ("TODO")
                 :tag "WORK"))
          (:name "Today: other"
            :and (:scheduled today
                 :todo ("TODO"))
            )
          (:name "Due today"
           :deadline today)
          (:name "Overdue"
           :and
           (:todo ("TODO")
            :deadline past))
          (:name "Scheduled earlier"
           :and (:scheduled past
                 :todo ("TODO")))
          (:name "Due soon"
           :deadline future)
          (:discard (:todo "HOLD"))
          )
        )
  (custom-set-faces '(org-super-agenda-header ((t (:foreground "dark orange" :weight bold :height: 1.3)))))
  (setq org-super-agenda-header-separator "\n---\n")
  (add-to-list 'warning-suppress-types '(org-element org-element-parser))
  (setq org-agenda-skip-scheduled-if-deadline-is-shown t)
  ;; Hide done habits to not clutter up the view
  (setq org-habit-show-all-today nil)
  )
#+end_src

*** calendar (WIP)
#+begin_src emacs-lisp :tangle no
(package! org-timeblock)
#+end_src
#+begin_src emacs-lisp :tangle config.el
(use-package! org-gcal
  :commands (org-gcal-fetch org-gcal-sync)
  :init
  (let* ((auth-info (car (auth-source-search :host "EmacsCall")))
         (secret (plist-get auth-info :secret)))
    (setq org-gcal-client-id (plist-get auth-info :user)
          org-gcal-client-secret (if (functionp secret) (funcall secret) secret)
          org-gcal-fetch-file-alist '(("arthurphilippeheymans@gmail.com" .  "~/org/cal.org")
                                      ("arthur.heymans@9elements.com" .  "~/org/9e_work.org")
                                      ("arthur@aheymans.xyz" .  "~/org/aheymans_cal.org")
                                      ("b6d6c10e3c9438a60cfa53c3c2c3a3b86fc81ee390a6d221445b79738c569488@group.calendar.google.com" . "~/org/gedeelde_cal.org"))))
  (setq plstore-cache-passphrase-for-symmetric-encryption t)
  :config
  (run-at-time nil (* 1 60 60) #'org-gcal-fetch)
  )
#+end_src


*** org-habits-stats
#+begin_src emacs-lisp :tangle packages.el
(package! org-habit-stats)
#+end_src
#+begin_src emacs-lisp :tangle config.el
(use-package! org-habit-stats
  :after (org org-agenda)
  :bind
  (("C-c h" . org-habit-stats-view-habit-at-point)
  :map org-agenda-mode-map
         ("H" . org-habit-stats-view-habit-at-point-agenda)))
(after! org
  (add-hook 'org-after-todo-state-change-hook 'org-habit-stats-update-properties))
#+end_src
** Org-roam & Knowledge Management
*** files
#+begin_src elisp :tangle config.el
(after! org
  (defun roam-extra:todo-p ()
    "Return non-nil if current buffer has any TODO entry."
    (org-element-map (org-element-parse-buffer 'headline) 'headline
      (lambda (h) (eq (org-element-property :todo-type h) 'todo))
      nil 'first-match))
  
  (defun roam-extra:update-todo-tag ()
    "Update TODO tag in the current buffer."
    (when (and (not (active-minibuffer-window))
               (org-roam-file-p))
      (let* ((tags (roam-extra:get-filetags))
             (is-todo (roam-extra:todo-p)))
        (cond ((and is-todo (not (seq-contains-p tags "todo")))
               (roam-extra:add-filetag "todo"))
              ((and (not is-todo) (seq-contains-p tags "todo"))
               (roam-extra:del-filetag "todo"))))))
  
  (defun roam-extra:get-filetags ()
    (split-string (or (org-roam-get-keyword "filetags") "")))
  
  (defun roam-extra:add-filetag (tag)
    (let* ((new-tags (cons tag (roam-extra:get-filetags)))
           (new-tags-str (combine-and-quote-strings new-tags)))
      (org-roam-set-keyword "filetags" new-tags-str)))
  
  (defun roam-extra:del-filetag (tag)
    (let* ((new-tags (seq-difference (roam-extra:get-filetags) `(,tag)))
           (new-tags-str (combine-and-quote-strings new-tags)))
      (org-roam-set-keyword "filetags" new-tags-str)))
  
  (defun roam-extra:todo-files ()
    "Return a list of roam files containing todo tag."
    (org-roam-db-sync)
    (let ((todo-nodes (seq-filter (lambda (n) (seq-contains-p (org-roam-node-tags n) "todo"))
                                  (org-roam-node-list))))
      (seq-uniq (seq-map #'org-roam-node-file todo-nodes))))
  
  (defun roam-extra:update-todo-files (&rest _)
    "Update the value of `org-agenda-files'.
Includes roam files with todo tag and specific additional files."
    (setq org-agenda-files
          (append (roam-extra:todo-files)
                  (mapcar (lambda (file) (expand-file-name file org-directory))
                          default-org-agenda-files))))
  
  (defvar default-org-agenda-files '("9e_work.org" "cal.org" "aheymans_cal.org" "gedeelde_cal.org"))
  
  (add-hook 'find-file-hook #'roam-extra:update-todo-tag)
  (add-hook 'before-save-hook #'roam-extra:update-todo-tag)
  (advice-add 'org-agenda :before #'roam-extra:update-todo-files)
  (setq org-agenda-hide-tags-regexp "todo")
  )
#+end_src

*** roam-ref
Capture files from browser bookmarklet
#+begin_src elisp :tangle config.el
(after! org
  (setq org-roam-capture-ref-templates
        '(("r" "ref" plain "%?" :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}") :unnarrowed t)
          ("n" "node" entry "* ${title}\n:PROPERTIES:\n:ID: %(org-id-new)\n:END:\n%?" :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}") :unnarrowed t)))
  )
#+end_src

*** roam-dailies meeting
#+begin_src elisp :tangle config.el
(after! org
  (setq org-roam-dailies-directory "daily"
        org-roam-dailies-capture-templates
        '(("d" "default" entry "* %?" :target (file+head "%<%Y-%m-%d>.org" "#+title: %<%Y-%m-%d>\n"))
          ("m" "meeting" plain
          "* Meeting: %^{Meeting Title|microsoft_daily|9elements}\n:PROPERTIES:\n:Date: %U\n:Participants: %^{Participants|Arthur Heymans}\n:END:\n** Agenda\n-\n\n** Notes\n- %?\n\n** Action Items\n- [ ]\n"
          :target (file+head "%<%Y-%m-%d>-meeting-%^{Meeting Title|microsoft_daily|9elements}.org" "#+title: %<%Y-%m-%d>\n")
           :unnarrowed t)
          )
        )
  )
#+end_src
** consult-org-roam
#+begin_src elisp :tangle packages.el
(package! consult-org-roam)
#+end_src

#+begin_src emacs-lisp :tangle config.el
(after! org-roam
  (consult-org-roam-mode 1)
  (setq consult-org-roam-grep-func #'consult-ripgrep)
  )
(use-package! org-roam
  :bind (("C-c n r c" . consult-org-roam-search)))
#+end_src
** org-babel
*** ob-async
#+begin_src elisp :tangle packages.el
(package! ob-async)
#+end_src
#+begin_src elisp :tangle config.el
(use-package! ob-async
  :after (org))
#+end_src

*** ob-gptel
Use gptel in org-babel source blocks
#+begin_src elisp :tangle packages.el
(package! ob-gptel :recipe (:host github :repo "ArthurHeymans/ob-gptel") :pin "def94fe7210a3170dbe2b447454399b610a11787")
#+end_src
#+begin_src elisp :tangle config.el
(use-package! ob-gptel
  :after (org gptel))
#+end_src

*** detached
detached allows running shell commands detached from Emacs using dtach.
Commands continue running even if Emacs crashes, with full output history.

Fix consult
#+begin_src elisp :tangle packages.el
(package! detached :recipe (:host sourcehut :repo "avph/detached.el") :pin "e4237ea6920b642cd30a460d532cff9ff84cf3e0")
#+end_src

#+begin_src elisp :tangle config.el
(use-package! detached
  :init
  (detached-init)
  :bind (;; Replace `async-shell-command' with `detached-shell-command'
         ([remap async-shell-command] . detached-shell-command)
         ;; Replace `compile' with `detached-compile'
         ([remap compile] . detached-compile)
         ([remap recompile] . detached-compile-recompile)
         ;; Replace built in completion of sessions with `consult'
         ([remap detached-open-session] . detached-consult-session))
  :custom ((detached-show-output-on-attach t)
           (detached-terminal-data-command system-type)))
#+end_src

**** Usage with org-babel
[[https://sr.ht/~niklaseklund/detached.el/#org-babel][org-babel docs]]
Add =:detached t= header argument to shell source blocks:

#+begin_example
,#+begin_src sh :detached t
  # Long running command
  sleep 60 && echo "Done!"
,#+end_src

,#+RESULTS:
: [detached]
#+end_example
* Development
** General
*** Rainbow delimiters
#+begin_src emacs-lisp :tangle packages.el
(package! rainbow-delimiters)
#+end_src
#+begin_src emacs-lisp :tangle config.el
(add-hook 'prog-mode-hook #'rainbow-delimiters-mode)
(after! rainbow-delimiters-mode
  (custom-set-faces
 '(rainbow-delimiters-depth-1-face ((t (:foreground "dark orange"))))
 '(rainbow-delimiters-depth-2-face ((t (:foreground "deep pink"))))
 '(rainbow-delimiters-depth-3-face ((t (:foreground "chartreuse"))))
 '(rainbow-delimiters-depth-4-face ((t (:foreground "deep sky blue"))))
 '(rainbow-delimiters-depth-5-face ((t (:foreground "yellow"))))
 '(rainbow-delimiters-depth-6-face ((t (:foreground "orchid"))))
 '(rainbow-delimiters-depth-7-face ((t (:foreground "spring green"))))
 '(rainbow-delimiters-depth-8-face ((t (:foreground "sienna1"))))))
#+end_src

** Language Support
*** Rust
Don't continue comment on the next line when. This is annoying
#+begin_src elisp :tangle config.el
(setq-hook! 'rust-mode-hook comment-line-break-function nil)
#+end_src

*** Kconfig Mode
Edit Kconfig like a pro
#+begin_src elisp :tangle packages.el
(package! kconfig-mode)
#+end_src

*** SystemRDL Mode
Syntax highlight for systemRDL
#+begin_src elisp :tangle packages.el
(package! systemrdl-mode
  :recipe (:host github :repo "luisgutz/emacs-system-rdl")
  :pin "b6889528a67cd169326422bfbdd6cdd031cbd09b")
#+end_src

#+begin_src elisp :tangle config.el
(use-package! systemrdl-mode)
#+end_src
*** KDL
#+begin_src elisp :tangle packages.el
(package! kdl-mode)
#+end_src
** Development Tools
*** Unsafe local settings
This allows us to set cargo lsp features in .dir-locals.el
#+begin_src elisp :tangle config.el
(put 'lsp-rust-features 'safe-local-variable #'vectorp)
(put 'lsp-rust-no-default-features 'safe-local-variable #'booleanp)
(setq safe-local-variable-values '((eval run-with-timer 0.1 nil #'sops-edit-file)
                                   (eval add-to-list 'mcp-hub-servers '("nix" :command "nix" :args ("run" "github:utensils/mcp-nixos" "--")))))
#+end_src
*** Rust feature setter
#+begin_src elisp :tangle packages.el
(package! toml :recipe (:host github :repo "ArthurHeymans/emacs-toml") :pin "2f41d7b6b243bb83d1a0fc28ba4811fc40a89ef1")
(package! lsp-cargo-feature-switcher :recipe (:host github :repo "ArthurHeymans/lsp-cargo-feature-switcher") :pin "3c9fe1e3a9c74d13c7c1fa287d23f4a76e54ba05")
#+end_src

*** LSP Configuration
Show types in lsp and allow for larger projects without being bothered all the time about it.

#+begin_src elisp :tangle config.el
(setq max-lisp-eval-depth 16000)
(after! lsp-mode
  (setq lsp-inlay-hint-enable t
        lsp-file-watch-threshold 6000)
  )
#+end_src

*** Build Tools
**** Justfile
#+begin_src elisp :tangle packages.el
(package! just-mode)
(package! justl)
#+end_src
#+begin_src elisp :tangle config.el
(after! justl
  (setq justl-shell 'eshell))
#+end_src

*** Version Control
**** Magit
magit-delta is too buggy for now.
#+begin_src elisp :tangle packages.el
;; (package! magit-delta :recipe (:host github :repo "dandavison/magit-delta") :pin "5fc7dbddcfacfe46d3fd876172ad02a9ab6ac616")
(package! gptel-magit :recipe (:host github :repo "ArthurHeymans/gptel-magit") :pin "42dc0836d9d7fd4066696c8a31c685ee8bb2f663")
(package! gptel-commit)
#+end_src
#+begin_src emacs-lisp :tangle config.el
(after! magit
  (setq transient-default-level 6)
  (setq git-commit-summary-max-length 72)
  (setq gptel-magit-body-length 72)
;;  (magit-delta-mode +1)
  )
#+end_src
**** emacs-pr-review
#+begin_src elisp :tangle packages.el
(package! pr-review)
#+end_src

* AI & Assistance
** API keys
#+begin_src elisp :tangle config.el
(unless (getenv "OPENAI_API_KEY")
  (setenv "OPENAI_API_KEY" (get-auth-info :user "apikey" :host "api.openai.com")))
(unless (getenv "ANTHROPIC_API_KEY")
  (setenv "ANTHROPIC_API_KEY" (get-auth-info :user "apikey" :host "api.anthropic.com")))
(unless (getenv "DEEPSEEK_API_KEY")
  (setenv "DEEPSEEK_API_KEY" (get-auth-info :user "apikey" :host "api.deepseek.com")))
(unless (getenv "OPENROUTER_API_KEY")
  (setenv "OPENROUTER_API_KEY" (get-auth-info :user "apikey" :host "openrouter.ai")))
#+end_src

** LLM Clients
*** Ellama
#+begin_src elisp :tangle packages.el
(package! ellama)
#+end_src

This gets all the models from openrouter and passes them to ellama.
Btw this code is generated by ellama.
#+begin_src elisp :tangle config.el
(defmacro add-openai-compatible-model (name id url)
  `(cons ,name  (make-llm-openai-compatible
                     :key (get-auth-info
                           :host (url-host (url-generic-parse-url ,url))
                           :user "apikey")
                     :url ,url
                     :chat-model ,id)))

(require 'json)
(require 'url)
(defun fetch-openai-compatible-models (url)
  (let* ((url-parsed (url-generic-parse-url url))
         (hostname (url-host url-parsed))
         (auth (auth-source-search :host hostname :user "apikey" :max 1))
         (token (funcall (plist-get (car auth) :secret))))
    (with-current-buffer
        (let ((url-request-extra-headers
               `(("Authorization" . ,(concat "Bearer " token)))))
          (url-retrieve-synchronously (concat url "/models")))
      (goto-char url-http-end-of-headers)
      (let* ((json-object-type 'alist)
             (json-data (json-read))
             (models (alist-get 'data json-data)))
        (mapcar (lambda (model)
                  (cons (or (alist-get 'name model)
                          (alist-get 'id model))
                        (alist-get 'id model)))
                models)))))

(after! ellama
  (setopt ellama-language "English")
  (require 'llm-openai)
  (setq ellama-sessions-directory "~/.emacs.d/.local/cache/ellama-sessions")
  (setq llm-warn-on-nonfree nil)
  (setq ellama-providers
        `(,@(let ((url "https://api.openai.com/v1"))
              (mapcar
               (lambda (model)
                 (add-openai-compatible-model (concat (car model) " [openai]") (cdr model) url))
               (fetch-openai-compatible-models url)))
          ,@(let ((url "https://openrouter.ai/api/v1"))
              (mapcar
               (lambda (model)
                 (add-openai-compatible-model (concat (car model) " [openrouter]") (cdr model) url))
               (fetch-openai-compatible-models url)))
          ,@(let ((url "https://api.deepseek.com"))
              (mapcar
               (lambda (model)
                 (add-openai-compatible-model (concat (car model) " [deepseek]") (cdr model) url))
               (fetch-openai-compatible-models url)))))
  (setq ellama-auto-scroll t)
  (setopt ellama-provider (eval (cdr (assoc "DeepSeek: DeepSeek V3 0324 [openrouter]" ellama-providers))))
  (setopt ellama-keymap-prefix "C-c z")
)

#+end_src

*** GPTEL
#+begin_src elisp :tangle config.el
(defun get-ollama-models ()
  "Fetch the list of installed Ollama models."
  (let* ((output (shell-command-to-string "ollama list"))
         (lines (split-string output "\n" t))
         models)
    (dolist (line (cdr lines))  ; Skip the first line
      (when (string-match "^\\([^[:space:]]+\\)" line)
        (push (match-string 1 line) models)))
    (nreverse models)))

(after! gptel
  (setq!
         gptel-default-mode 'org-mode
         gptel-magit-model 'anthropic/claude-sonnet-4.5
         gptel-model 'anthropic/claude-sonnet-4.5
         gptel-backend
         (gptel-make-openai "OpenRouter"               ;Any name you want
           :host "openrouter.ai"
           :endpoint "/api/v1/chat/completions"
           :stream t
           :key (gptel-api-key-from-auth-source "openrouter.ai")
           :models (mapcar (lambda (model)
                              (cdr model))
                            (fetch-openai-compatible-models "https://openrouter.ai/api/v1")))
         gptel-org-branching-context t)
  (add-hook 'gptel-post-stream-hook 'gptel-auto-scroll)
  ;; DeepSeek offers an OpenAI compatible API
  (gptel-make-openai "DeepSeek"       ;Any name you want
    :host "api.deepseek.com"
    :endpoint "/chat/completions"
    :stream t
    :key (gptel-api-key-from-auth-source "api.deepseek.com")
    :models '("deepseek-chat" "deepseek-reasoner"))
  (gptel-make-openai "OpenRouter"               ;Any name you want
    :host "openrouter.ai"
    :endpoint "/api/v1/chat/completions"
    :stream t
    :key (gptel-api-key-from-auth-source "openrouter.ai")
    :models (mapcar (lambda (model)
                      (cdr model))
               (fetch-openai-compatible-models "https://openrouter.ai/api/v1")))
  (gptel-make-openai "OpenAI"             ;Any name you want
    :host "api.openai.com"
    :endpoint "/v1/chat/completions"
    :stream t
    :key (gptel-api-key-from-auth-source "api.openai.com")
    :models (mapcar (lambda (model)
                      (cdr model))
               (fetch-openai-compatible-models "https://api.openai.com/v1")))
  (gptel-make-anthropic "Claude"          ;Any name you want
    :stream t                             ;Streaming responses
    :key (gptel-api-key-from-auth-source "api.anthropic.com"))
  (gptel-make-ollama "Ollama"             ;Any name of your choosing
    :host "localhost:11434"               ;Where it's running
    :stream t                             ;Stream responses
    :models (get-ollama-models))          ;List of models
  )
(map! :leader
      (:prefix ("G" . "gptel")
       :desc "Start GPTel" "g" #'gptel
       :desc "GPTel menu" "m" #'gptel-menu
       :desc "Send to GPTel" "s" #'gptel-send
       :desc "Connect to MCP" "c" #'gptel-mcp-connect
       :desc "Disconnect from MCP" "d" #'gptel-mcp-disconnect))

(require 'gptel-integrations)
#+end_src

**** MCP Client
Connect to external MCP servers from Emacs using mcp.el
#+begin_src elisp :tangle packages.el
(package! mcp)
#+end_src

#+begin_src elisp :tangle config.el
(setq mcp-hub-servers
      `(("filesystem" . (:command "npx" :args ("-y" "@modelcontextprotocol/server-filesystem")))
        ("fetch" . (:command "uvx" :args ("mcp-server-fetch")))
        ("time" . (:command "uvx" :args ("mcp-server-time" "--local-timezone=Europe/Brussels")))
        ("github" . (:command "podman"
                      :args ("run" "-i" "--rm" "-e" "GITHUB_PERSONAL_ACCESS_TOKEN" "ghcr.io/github/github-mcp-server")
                      :env (:GITHUB_PERSONAL_ACCESS_TOKEN ,(get-auth-info :user "secret" :host "github-token"))))
        ("OSM" . (:command "uvx" :args ("osm-mcp-server")))
        ("emacs" . (:command "socat" :args ("-" "UNIX-CONNECT:/home/arthur/.config/emacs-mcp/emacs-mcp-server.sock")))
        ("HA-hamelstraat" . (:command "mcp-proxy"
                              :env (:SSE_URL "https://hamelstraat.duckdns.org:8123/mcp_server/sse"
                                    :API_ACCESS_TOKEN ,(get-auth-info :user "token" :host "hamelstraat"))))))
#+end_src

**** MCP Server
Turn Emacs into an MCP server that LLMs can connect to
#+begin_src elisp :tangle packages.el
(package! mcp-server :recipe (:host github :repo "rhblind/emacs-mcp-server" :files ("*.el" "mcp-wrapper.py" "mcp-wrapper.sh")) :pin "e5edc3e064ed6d8d6a0921f43ca577f7e7a17856")
#+end_src

#+begin_src elisp :tangle config.el
;; Start Emacs MCP Server on startup
(use-package! mcp-server
  :config
  (setq mcp-server-socket-directory "~/.config/emacs-mcp")
  (add-hook 'emacs-startup-hook #'mcp-server-start-unix))
#+end_src
*** Agent-shell
A native Emacs shell to interact with LLM agents powered by ACP (Agent Client Protocol).
#+begin_src elisp :tangle packages.el
(package! shell-maker)
(package! acp :recipe (:host github :repo "xenodium/acp.el") :pin "f9dd828c7bfb209523e907cc947668924ff9f1bf")
(package! agent-shell :recipe (:host github :repo "xenodium/agent-shell") :pin "60a8a757eb01ef76c42a49b629793c4651977ec3")
#+end_src

#+begin_src elisp :tangle config.el
(after! agent-shell
  (require 'acp)
  (require 'agent-shell)
  (setq agent-shell-google-authentication
      (agent-shell-google-make-authentication :login t))
  (setq agent-shell-openai-authentication
      (agent-shell-openai-make-authentication :api-key (get-auth-info :user "apikey" :host "api.openai.com")))
)
#+end_src

*** ECA
#+begin_src elisp :tangle packages.el
(package! eca :recipe (:host github :repo "editor-code-assistant/eca-emacs" :files ("*.el")) :pin "25e9f5762e4bbf1d2f2189d738a01ab0c172b446")
#+end_src
#+begin_src emacs-lisp :tangle config.el
(after! eca
  (setq eca-chat-use-side-window nil))
#+end_src
*** Whisper
#+begin_src elisp :tangle packages.el
(package! whisper :recipe (:host github :repo "natrys/whisper.el" :files ("whisper.el" "whisper-languages.el"))
  :pin "6198ce3d9bff0555cf098a77b78d6c2d79baf4f9")
#+end_src
#+begin_src elisp :tangle config.el
(after! whisper
  (setq whisper-server-mode 'openai
        whisper-openai-api-key (get-auth-info :user "apikey" :host "api.openai.com"))
  )
(map! "C-S-h" #'whisper-run)
#+end_src
* Communication & Social
** Email
*** Mu4e
Show account in view.

#+begin_src elisp :tangle config.el
(setq +mu4e-gmail-accounts '(("arthur@aheymans.xyz" . "/aheymans")
                            ("arthurphilippeheymans@gmail.com" . "/gmail")
                            ("arthur.heymans@9elements.com" . "/9elements")))
(setq mu4e-headers-fields
      '((:account-stripe . 1) (:account . 10) (:human-date . 12) (:flags . 6) (:from-or-to . 25) (:subject)))
(setq mu4e-maildir-shortcuts
  '((:maildir "/aheymans/coreboot" :key  ?c)
    (:maildir "/aheymans/gerrit"   :key  ?i)
    (:maildir "/aheymans/github"   :key  ?g)))
(after! mu4e
  (setq mu4e-update-interval (* 15 60)))
(setq user-full-name "Arthur Heymans"
      user-mail-address "arthur@aheymans.xyz")
(setq send-mail-function 'smtpmail-send-it)
(set-email-account! "gmail"
                    '((user-mail-address      . "arthurphilippeheymans@gmail.com")
                      (user-full-name         . "Arthur Heymans")
                      (smtpmail-smtp-server   . "smtp.gmail.com")
                      (smtpmail-smtp-user     . "arthurphilippeheymans@gmail.com")
                      (smtpmail-smtp-service  . 587)
                      (smtpmail-stream-type   . starttls)
                      (smtpmail-debug-info    . t)
                      (mu4e-sent-folder       . "/gmail/[Gmail]/Sent Mail")
                      )
                    nil)
(set-email-account! "aheymans"
                    '((user-mail-address      . "arthur@aheymans.xyz")
                      (user-full-name         . "Arthur Heymans")
                      (smtpmail-smtp-server   . "smtp.gmail.com")
                      (smtpmail-smtp-user     . "arthur@aheymans.xyz")
                      (smtpmail-smtp-service  . 587)
                      (smtpmail-stream-type   . starttls)
                      (smtpmail-debug-info    . t)
                      (mu4e-sent-folder       . "/aheymans/[Gmail]/Sent Mail")
                      )
                    t)
(set-email-account! "9elements"
                    '((user-mail-address      . "arthur.heymans@9elements.com")
                      (user-full-name         . "Arthur Heymans")
                      (smtpmail-smtp-server   . "smtp.gmail.com")
                      (smtpmail-smtp-user     . "arthur.heymans@9elements.com")
                      (smtpmail-smtp-service  . 587)
                      (smtpmail-stream-type   . starttls)
                      (smtpmail-debug-info    . t)
                      (mu4e-sent-folder       . "/9elements/[Gmail]/Sent Mail")
                      )
                    nil)

#+end_src

** Chat
*** IRC
Do socials like neckbears
#+begin_src elisp :tangle config.el
(after! circe
  (defun my-nickserv-password (server)
    (get-auth-info :user "avph" :machine "irc.libera.chat"))

  (setq circe-network-options
         '(("Libera Chat"
            :nick "avph"
            :sasl-username "avph"
            :sasl-password my-nickserv-password
            :channels ("#flashprog")))))
#+end_src

*** Matrix
#+begin_src elisp :tangle packages.el
(package! ement)
#+end_src

** Media
*** Gif Screencast
#+begin_src elisp :tangle packages.el
(package! gif-screencast)
#+end_src

#+begin_src elisp :tangle config.el
(after! gif-screencast
  (setq gif-screencast-screenshot-directory "~/Pictures/screencasts/"
        gif-screencast-program "flameshot"
        gif-screencast-args '()))
#+end_src

*** Spotify
Do spotify from emacs
#+begin_src elisp :tangle packages.el
;; Override for oauth fix, keep track of https://github.com/danielfm/smudge/pull/106/commits
(package! smudge :recipe (:host github :repo "JoaoZPalma/smudge") :pin "6be8d1eaa800200f2547c90844e5c198b22277e9")
#+end_src

#+begin_src elisp :tangle config.el
(map! :leader
      :desc "Spotify" "S" #'smudge-command-map)
(use-package! smudge
  :custom
  (smudge-oauth2-client-secret (get-auth-info :user "secret" :host "spotify"))
  (smudge-oauth2-client-id (get-auth-info :user "id" :host "spotify"))
  ;; optional: enable transient map for frequent commands
  (smudge-player-use-transient-map t)
  (smudge-transport 'connect)
  (defhydra hydra-spotify (:hint nil)
    "
^Search^                  ^Control^               ^Manage^
^^^^^^^^-----------------------------------------------------------------
_t_: Track               _SPC_: Play/Pause        _+_: Volume up
_m_: My Playlists        _n_  : Next Track        _-_: Volume down
_f_: Featured Playlists  _p_  : Previous Track    _x_: Mute
_u_: User Playlists      _r_  : Repeat            _d_: Device
^^                       _s_  : Shuffle           _q_: Quit
"
    ("t" smudge-track-search :exit t)
    ("m" smudge-my-playlists :exit t)
    ("f" smudge-featured-playlists :exit t)
    ("u" smudge-user-playlists :exit t)
    ("SPC" smudge-controller-toggle-play :exit nil)
    ("n" smudge-controller-next-track :exit nil)
    ("p" smudge-controller-previous-track :exit nil)
    ("r" smudge-controller-toggle-repeat :exit nil)
    ("s" smudge-controller-toggle-shuffle :exit nil)
    ("+" smudge-controller-volume-up :exit nil)
    ("-" smudge-controller-volume-down :exit nil)
    ("x" smudge-controller-volume-mute-unmute :exit nil)
    ("d" smudge-select-device :exit nil)
    ("q" quit-window "quit" :color blue))

  (bind-key "a" #'hydra-spotify/body smudge-command-map)
  )
#+end_src

** Torrent: transmission
#+begin_src elisp :tangle packages.el
(package! transmission)
#+end_src

** Reddit
#+begin_src elisp :tangle packages.el
(package! reddigg)
#+end_src

#+begin_src elisp :tangle config.el
(after! reddigg
  (setq reddigg-subs '(emacs localllama rust chatgptcoding researchchemicals)))
#+end_src

* System Integration
** Security
*** GnuPG
Use emacs epg for gpg pinentry. Make sure allow-emacs-pinentry is not set.
#+begin_src elisp :tangle config.el
(setq epg-pinentry-mode 'loopback)
(pinentry-start)
#+end_src

#+begin_src elisp :tangle packages.el
(package! pinentry)
#+end_src

*** SOPS
#+begin_src elisp :tangle packages.el
(package! sops)
#+end_src

** Desktop Integration
*** KDE Connect
#+begin_src elisp :tangle packages.el
(package! kdeconnect)
#+end_src
** proced
Don't use htop
#+begin_src emacs-lisp :tangle config.el
(after! proced
  (setq proced-enable-color-flag t)
  (add-to-list
   'proced-format-alist
   '(custom user pid ppid sess tree pcpu pmem rss start time state (args comm)))
  (setq-default proced-format 'custom))
#+end_src
** App launcher in OS
#+begin_src emacs-lisp :tangle packages.el
(package! app-launcher :recipe (:host github :repo "SebastienWae/app-launcher") :pin "d5015e394b0a666a8c7c4d4bdf786266e773b145")
#+end_src

* Terminal & Shell
** Terminal Emulators
*** aweshell
#+begin_src elisp :tangle packages.el
(package! aweshell :recipe (:host github :repo "manateelazycat/aweshell")
  :pin "db495f29eef9013cf6b3796c3797e0ec76352e3f")
#+end_src
#+begin_
#+begin_src elisp :tangle config.el
(use-package! aweshell
  :bind (("C-c t n" . aweshell-new)
         ("C-c t p" . aweshell-prev)
         ("C-c t t" . aweshell-next)
         ("C-c t c" . aweshell-clear-buffer)
         ("C-c t s" . aweshell-sudo-toggle)
         ("C-c t d" . aweshell-dedicated-toggle))
  :config
  (setq aweshell-use-exec-path-from-shell t
        aweshell-dedicated-window-height 30))
#+end_src

*** eshell

#+begin_src elisp :tangle config.el
(add-hook 'eshell-mode-hook (lambda () (setenv "TERM" "xterm-256color")))
(setq eshell-prompt-function
      (lambda ()
        (concat
         (propertize "┌─[" 'face `(:foreground "green"))
         (propertize (user-login-name) 'face `(:foreground "red"))
         (propertize "@" 'face `(:foreground "green"))
         (propertize (system-name) 'face `(:foreground "blue"))
         (propertize "]──[" 'face `(:foreground "green"))
         (propertize (format-time-string "%H:%M" (current-time)) 'face `(:foreground "yellow"))
         (propertize "]──[" 'face `(:foreground "green"))
         (propertize (concat (eshell/pwd)) 'face `(:foreground "white"))
         (propertize "]\n" 'face `(:foreground "green"))
         (propertize "└─>" 'face `(:foreground "green"))
         (propertize (if (= (user-uid) 0) " # " " $ ") 'face `(:foreground "white"))
         )))
#+end_src

*** eat
#+begin_src elisp :tangle packages.el
(package! eat)
#+end_src
#+begin_src elisp :tangle config.el
(after! eat
    (custom-set-faces
    `(eat-term-color-black   ((t (:foreground "#0d0d0d" :background "#0d0d0d"))))
    `(eat-term-color-red     ((t (:foreground "#FF301B" :background "#FF4352"))))
    `(eat-term-color-green   ((t (:foreground "#A0E521" :background "#B8E466"))))
    `(eat-term-color-yellow  ((t (:foreground "#FFC620" :background "#FFD750"))))
    `(eat-term-color-blue    ((t (:foreground "#1BA6FA" :background "#1BA6FA"))))
    `(eat-term-color-magenta ((t (:foreground "#8763B8" :background "#A578EA"))))
    `(eat-term-color-cyan    ((t (:foreground "#21DEEF" :background "#73FBF1"))))
    `(eat-term-color-white   ((t (:foreground "#EBEBEB" :background "#FEFEF8")))))
    (setq eat-term-scrollback-size (* 20 1024 1024))
  )
#+end_src

* Navigation & Search
** Search Tools
*** Consult
#+begin_src elisp :tangle config.el
(map! :leader
      :desc "Run consult-ripgrep"
      "gr" #'consult-ripgrep)
#+end_src
* Temporary fixes
#+begin_src elisp :tangle packages.el
(package! centered-window :recipe (:host github :repo "emacsmirror/centered-window") :pin "701f56cd1d2b68352d29914f05ca1b0037bb2595")
#+end_src
